/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */options{  static = true;}PARSER_BEGIN(WhileNullParser)
import java.io.*;
import java.util.*;
import java.lang.*;
import java.nio.file.*;
import java.nio.charset.*;

// Compiler-Part: https://github.com/albertkasdorf/TI-Praktikum

public class WhileNullParser {

	public static Map<String, String> SymbolTable = new HashMap<String, String>();

  	public static void main(String args[]) {		
		if(args.length == 0){
		  	try {		  	  
				System.out.println ("ExampleParser: Reading input ...");
				WhileNullParser parser = new WhileNullParser(System.in);
				parser.program();

				
				System.out.println ("Gültig");

				
			}
			catch (Exception e) {
				System.out.println(e.getMessage());
				System.out.println("Kein gültiges Programm");			
			}
		}
		else if(args.length == 1){
			System.out.println ("ExampleParser: Reading the file " + args[0] + " ..." );
			try {	
                WhileNullParser parser = new WhileNullParser(new java.io.FileInputStream(args[0]));
				parser.program();
				
				System.out.println ("Gültig");
				printUrm();
				writeUrm();
			}
			catch(Exception e) {
				System.out.println(e.getMessage());
				return;
			}
		}
		else {
			System.out.println ("ExampleParser: You must use one of the following:");
			System.out.println (" java ExampleParser < file");
			System.out.println ("Or");
			System.out.println (" java ExampleParser file");
			return ;
		}
	}

	/// ----------- Compiler Logik
	// Programm-Name
	private static String FProgramName;	

	// Enthält das URM Programm
	private static ArrayList<String > program = new ArrayList<String >(); 

	// Sprungmarken
	private static Stack<AbstractMap.SimpleEntry<Integer, Integer > > while_replace = new Stack<AbstractMap.SimpleEntry<Integer, Integer > >();

	// Übersetzung Variable - > Register
	static int RegisterCounter = 2;
	public static void AddIdent(Token token) {
	  	String hVariableName = token.image;
	  	
		if (SymbolTable.containsKey(hVariableName)) {
		  // Exception, duplicate
		}
		else {
		  SymbolTable.put(hVariableName, "R"+RegisterCounter);
		  RegisterCounter = RegisterCounter + 1;		  		  
		}	
	}

	private static void AddOutIdent(Token token) throws ParseException
	{
		String name = token.image;

		if(SymbolTable.containsKey(name))
			throw new ParseException("Multiple out variables not allowed.");

		SymbolTable.put(name, "R1");
	}

	
	private static String GetRegister(String variableName) throws ParseException {
		if(!SymbolTable.containsKey(variableName))
			throw new ParseException("Variable " + variableName + " nicht definiert.");

		return SymbolTable.get(variableName);
	}

	private static String GetUnusedRegister() {
		return String.format("R%d", RegisterCounter++);
	}


	//---- Übersetzung der syntaktischen Einheiten ----

	// Rx = 0	
	private static void createNullAssignment(Token token) throws ParseException
	{
		String register = GetRegister(token.image);

		program.add(String.format("; %s = 0", token.image));
		program.add(String.format("%s = 0", register));
	}

	// Rx = Ry++
	private static void createIncrementAssignment(Token lToken, Token rToken) throws ParseException
	{
		program.add("; " + lToken.image + " = " + rToken.image + " + 1");

		String l_reg = GetRegister(lToken.image);
		String r_reg = GetRegister(rToken.image);
		String h_reg = GetUnusedRegister();

		createCopy(h_reg, r_reg);	
		program.add(String.format("%s++", h_reg));
		createCopy(l_reg, h_reg);// Ggf. noch kürzer machen indem ich direkt nach l_reg kopiere
	}

	// WHILE
	private static void createWhileBegin(Token lToken, Token rToken) throws ParseException
	{
		String l_copy_reg = GetUnusedRegister();
		String r_copy_reg = GetUnusedRegister();

		int copy_base_line = getNextLineNumber();
		createCopy(l_copy_reg, GetRegister(lToken.image));
		createCopy(r_copy_reg, GetRegister(rToken.image));

		int compare_l_copy_reg = getNextLineNumber();
		program.add(String.format("if %s == 0 goto %s", l_copy_reg, compare_l_copy_reg + 2));
		program.add(String.format("goto %s", compare_l_copy_reg + 4));

		while_replace.push(new AbstractMap.SimpleEntry<Integer, Integer >(program.size() , -1));
		program.add(String.format("if %s == 0 goto <??>", r_copy_reg));   // go to end. end der Schleife ist noch unbekannt
		program.add(String.format("goto %s", compare_l_copy_reg + 8));
		program.add(String.format("if %s == 0 goto %s", r_copy_reg, compare_l_copy_reg + 8));
		program.add(String.format("%s--", l_copy_reg));
		program.add(String.format("%s--", r_copy_reg));
		program.add(String.format("goto %d", compare_l_copy_reg));

		while_replace.push(new AbstractMap.SimpleEntry<Integer, Integer >(-1 , copy_base_line));
	}

	private static void createWhileEnd() throws ParseException
	{
		AbstractMap.SimpleEntry<Integer, Integer> jump_to_copy = while_replace.pop();
		AbstractMap.SimpleEntry<Integer, Integer> jump_to_end = while_replace.pop();

		program.add(String.format("goto %d", jump_to_copy.getValue()));

		int program_index = jump_to_end.getKey();
		String program_line = program.get(program_index);
		program_line = program_line.replace("<??>", String.format("%d", getNextLineNumber()));
		program.set(program_index, program_line);
	}

	// Helper
	private static void createCopy(String xReg, String yReg) throws ParseException
	{
		String zReg = GetUnusedRegister();

		// Nummer der ersten Programmzeile von copy
		int base_line = getNextLineNumber();


		program.add(String.format("%s = 0", xReg));
		program.add(String.format("if %s == 0 goto %d", yReg, base_line + 5));
		program.add(String.format("%s--", yReg));
		program.add(String.format("%s++", zReg));
		program.add(String.format("goto %d", base_line + 1));
		program.add(String.format("if %s == 0 goto %d", zReg, base_line + 10));
		program.add(String.format("%s--", zReg));
		program.add(String.format("%s++", xReg));
		program.add(String.format("%s++", yReg));
		program.add(String.format("goto %d", base_line + 5));
	}

	private static int getNextLineNumber()
	{
		int line_number = 1;
		for(String line : program)
		{
			if(line.startsWith(";"))
				continue;
			else
				line_number++;
		}
		return line_number;
	}

	private static void CreateVarRegMapping()
	{
		StringBuilder sb = new StringBuilder();
		
		sb.append("; ");
		for(Map.Entry<String, String > entry : SymbolTable.entrySet())
		{
			sb.append(entry.getKey() + " -> " + entry.getValue() + ", ");
		}
		program.add(sb.toString());
	}

	// Ausgabe
	public static void printUrm()
	{
		int line_number = 1;
		for(String line : program)
		{
			if(line.startsWith(";"))
				System.out.println(String.format("--: %s", line));
			else
				System.out.println(String.format("%02d: %s", line_number++, line));
		}
	}

	public static void writeUrm() throws IOException
	{
		OpenOption[] options = new OpenOption[] { StandardOpenOption.CREATE, StandardOpenOption.TRUNCATE_EXISTING, StandardOpenOption.WRITE };
		Path file = Paths.get("", "urm", FProgramName + ".urm");
		file = file.toAbsolutePath();
		Files.write(file, program, Charset.forName("UTF-8"), options);
	}


}
PARSER_END(WhileNullParser)TOKEN :
{
	< IN : "in">
	|
	< NULL : "0">
	|
	< EINS : "1">
	|
	< DO : "do">
	|
	< END : "end">
	|
	< BEGIN : "begin">
	|
	< VAR : "var">
	|
	< OUT : "out">
	|
	< WHILE : "while">
	|
	<IDENT: (["A"-"Z"]|["a"-"z"]|["0"-"9"]|["Ä"]|["Ö"]|["Ü"])+>
}


SKIP :
{
	" "
	| "\n" 
	| "\t"
	| "\r"
}


void program():
{
	Token t;
}
{
	t = <IDENT> { FProgramName = t.image; }
	"(" inputVars() ";" outputVars() ")" ";" vars() ";" { CreateVarRegMapping(); } statement() 
}
void inputVars():
{
	Token t;
}
{
	<IN> t = <IDENT> { AddIdent(t); } ("," t = <IDENT> { AddIdent(t); })*
}
void outputVars():
{
	Token t;
}
{
	<OUT> t = <IDENT> { AddOutIdent(t); }
}
void vars():
{
	Token t;
}
{
	<VAR> "(" t = <IDENT> { AddIdent(t); } ("," t = <IDENT> { AddIdent(t); })*")"
}


void assignment():
{
  Token tokLValue, tokRValue;
}
{
	tokLValue=<IDENT>
	"="
	(
	  	<NULL>	{ createNullAssignment(tokLValue);  }
	|
		(tokRValue = <IDENT>
	  	"+"
	  	<EINS>) { createIncrementAssignment(tokLValue, tokRValue);	}
  	)

}


void whileStmnt():
{
  Token tokLValue, tokRValue;
}
{
	<WHILE> tokLValue = <IDENT> "!=" tokRValue = <IDENT> <DO> <BEGIN> { createWhileBegin(tokLValue, tokRValue); } statement() <END> { createWhileEnd(); }
}
void statement():
{
	
}
{
	(assignment() {}
		|
	whileStmnt()
	)

	(";" statement())?
}